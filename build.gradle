group 'com.blackboard.gradle'
version '1.2-SNAPSHOT'
description 'Gradle plugin for running JMH Benchmarks'

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'jacoco'
apply plugin: 'idea'
apply plugin: 'errorprone'
apply plugin: 'versions'
apply plugin: 'maven'
apply plugin: 'eclipse'

apply from: 'gradle/dependencies.gradle'
apply from: 'gradle/idea.gradle'
apply from: 'gradle/integTest.gradle'

repositories {
  mavenCentral()
}

dependencies {
  compile gradleApi()
  testCompile libraries.testlibs
}

task wrapper(type: Wrapper) { gradleVersion = '1.11' }

defaultTasks 'build'

buildscript {
  repositories {
    maven { url 'https://github.com/ben-manes/gradle-versions-plugin/raw/mvnrepo' }
    mavenCentral()
  }

  dependencies {
    classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.3'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.5-beta-2'
  }
}

uploadArchives {
  def nexusUsername = "$System.env.NEXUS_USERNAME".trim()
  def nexusPassword = "$System.env.NEXUS_PASSWORD".trim()

  if (project.hasProperty("jenkinsMavenUsername")) {
    nexusUsername = jenkinsMavenUsername
  }

  if (project.hasProperty("jenkinsMavenPassword")) {
    nexusPassword = jenkinsMavenPassword
  }
  repositories {
    mavenDeployer {
      repository( url : "http://maven.pd.local/content/repositories/releases/" ) {
        authentication( userName: nexusUsername, password: nexusPassword )
      }
      snapshotRepository( url : "http://maven.pd.local/content/repositories/releases/" ) {
        authentication( userName: nexusUsername, password: nexusPassword )
      }
    }
  }
}
